# =========================================================================
# 
#   Program:   Insight Segmentation & Registration Toolkit
#   Module:    Test.tcl.in
#   Language:  Tcl
#   Date:      $Date$
#   Version:   $Revision$
# 
# 
#   Copyright (c) 2000 National Library of Medicine
#   All rights reserved.
# 
#   See COPYRIGHT.txt for copyright details.
# 
# =========================================================================*/

source [file join Testing Utilities Utility.conf]
source [file join Testing Utilities Utility.tcl]

set Model Experimental
if { $argc > 0 } \
{
  set Model [lindex $argv 0]
}

set HTMLDir [file join Testing HTML]
set TempDir [file join Testing Temporary]

set SiteDir [file join $HTMLDir TestingResults Sites $Site]
set BuildNameDir [file join $SiteDir $BuildName]

# Find the build name directory
set BuildStampDir [GetLastBuildDirectory $Model]
if { $BuildStampDir == "" } \
{
  puts  "\tCould not find any builds in the $BuildNameDir to do testing $Result"
  exit 1
}

set BuildStamp [file tail $BuildStampDir]
set XMLDir [file join $BuildStampDir XML]

set Out [open [file join $XMLDir Test.xml] w]

puts $Out {<?xml version="1.0" encoding="UTF-8"?>}
# puts $Out {<!DOCTYPE Insight>}
puts $Out "<Site BuildName=\"$BuildName\" BuildStamp=\"$BuildStamp\" Name=\"$Site\">"

puts $Out {<Testing>}
puts $Out "\t<StartDateTime>[clock format [clock seconds]]</StartDateTime>"


proc FindTests { Path } \
{
  global TestList
  set Tests [GetCMakeVariable TESTS]
  foreach Test $Tests \
  {
    lappend TestList "[file join $Path $Test]"
  }
  foreach SubDir [GetCMakeVariable SUBDIRS] \
  {
    cd $SubDir
    FindTests [file join $Path $SubDir]
    cd ..
  }
}

# Find the list of canidate tests
puts "\tFinding Tests"
set TestList ""
set OldDir [pwd]
cd $SourceDirectory
FindTests .
cd $OldDir
puts "\tFound [llength $TestList] tests"

proc RemoveFile { file } \
{
  file delete -force -- $file
}

# Delete any old coverage files
catch { FileMap [glob -nocomplain *] [list *.da *.gcov] RemoveFile } Result

# Write the list
puts $Out "\t<TestList>"
foreach Test $TestList \
{
  puts $Out "\t\t<Test>[XMLSafeString $Test]</Test>"
}
puts $Out "\t</TestList>"

proc CheckFile { Filename } \
{
  return [expr [file exists $Filename] && [file executable $Filename]]
}

# For each test, cd to the directory, and run it.
puts "\tRunning Tests"
set ReportPassed 0
set ReportFailed 0
set ReportNotRun 0
foreach Test $TestList \
{
  cd [file dirname $Test]

  set Filename [file tail $Test]

  switch $tcl_platform(platform) \
  {
    windows \
    {
      if { [regexp cygtclsh [info nameofexecutable]] == 0 } {
        # Windows build
	set NewFilename [file join Release $Filename.exe]
      } else {
        # Cygwin build
        set NewFilename [file join . $Filename]
      }
    }
    default \
    {
      set NewFilename [file join . $Filename]
    }
  }
  
  set Status notrun
  set Result ""
  if { [CheckFile $NewFilename] } \
  {
    set Status [catch { eval exec $NewFilename } Result]
  }
  if { $Status == 0 } \
  {
    set Status passed
  }
  if { $Status == 1 } \
  {
    set Status failed
  }
  switch $Status \
  {
    notrun { incr ReportNotRun }
    passed { incr ReportPassed }
    failed { incr ReportFailed }
  }
  
  puts $Out "\t<Test Status=\"$Status\">"
  puts $Out "\t\t<Name>[XMLSafeString $Filename]</Name>"
  puts $Out "\t\t<Path>[XMLSafeString [file dirname $Test]]</Path>"
  puts $Out "\t\t<FullName>[XMLSafeString $Test]</FullName>"
  puts $Out "\t\t<Results>"
  puts $Out "\t\t\t<Measurement>"
  puts $Out "\t\t\t\t<Value>[XMLSafeString $Result]</Value>"
  puts $Out "\t\t\t</Measurement>"
  puts $Out "\t\t</Results>"
  puts $Out "\t</Test>"

  cd $OldDir

}

puts $Out "\t<EndDateTime>[clock format [clock seconds]]</EndDateTime>"

puts $Out "</Testing>"
puts $Out "</Site>"
puts "\tTesting completed"
puts "\t$ReportNotRun Tests Not Run"
puts "\t$ReportFailed Tests Failed"
puts "\t$ReportPassed Tests Passed"

close $Out
exit

