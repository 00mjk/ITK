# =========================================================================
# 
#   Program:   Insight Segmentation & Registration Toolkit
#   Module:    DashboardManager.tcl.in
#   Language:  Tcl
#   Date:      $Date$
#   Version:   $Revision$
# 
# 
#   Copyright (c) 2000 National Library of Medicine
#   All rights reserved.
# 
#   See COPYRIGHT.txt for copyright details.
# 
# =========================================================================*/

#
# DashboardManager.tcl <Model> <Command1> [Command2]...
#
# Models:
#
# Experimental  -- Developer driven testing
# Nightly       -- Nightly build in lockstep with all other nightly builds
# Dashboard     -- Dashboard mode, builds the the nightly rollup
#
# Commands for Experimental and Nightly:
#
# Start         -- Creates the proper directory
# Update        -- Perform a cvs update for a nightly to the proper version of the code
#                  Report on local changes for Experimental builds
# Build         -- Do the build and report in the proper results directory
# Test          -- Do the testing and report in the proper results directory
# Coverage      -- Summerize coverage information
# End           -- Build the HTML pages
#
# Commands for Dashboard building:
#
# DashboardStart [date]  -- Starts a dashboard for the given date
#                         Yesterday is default, only for nightly
# DashboardEnd   [date]  -- Ends a dashboard for the given date
#                         Yesterday is default, only for nightly

set Usage {usage: $argv0 <Model> <Command1> [Command2]...}
if { $argc < 2 } \
{
  puts stderr $Usage
  exit 1
}

source [file join Testing Utilities Utility.tcl]
source [file join Testing Utilities Utility.conf]
source [file join $SourceDirectory Testing Utilities base64.tcl]

set TestingBaseDir [file join Testing HTML TestingResults]

set Model [lindex $argv 0]
set Commands [lrange $argv 1 end]

# Must be run in build directory
set BuildStampBase [file join $TestingBaseDir Sites $Site $BuildName]
set UtilitiesDir [file join Testing Utilities]

set DateTimeStamp [MakeDateTimeStamp]
set NightlyDateStamp [MakeNightlyDateStamp]


switch -glob -- $Model \
{
  Continuous - \
  Exp* \
  {
    set BuildStampDir [file join $BuildStampBase $DateTimeStamp]
    set DashboardDir [file join $TestingBaseDir Dashboard $DateTimeStamp]
  }
  Nig* \
  {
    set BuildStampDir [file join $BuildStampBase ${NightlyDateStamp}-Nightly]
    set DashboardDir [file join $TestingBaseDir Dashboard ${NightlyDateStamp}-Nightly]
  }
  default \
  {
    puts stderr "Unknown Model: $Model"
    exit 1
  }
}

set XMLDir [file join $BuildStampDir XML]
set XSLDir [file join $SourceDirectory Testing Utilities XSL]

foreach Command $Commands \
{
  switch $Command \
  {
    Start \
    {
      puts "Starting $Model Build [file tail $BuildStampDir]"
      file mkdir $BuildStampDir $XMLDir
    }
    Build - \
    Test - \
    Coverage - \
    Purify - \
    Submit \
    {
      puts "$Command $Model"
      set f [open "|$TclshCommand [file join $UtilitiesDir $Command.tcl ] $Model" r]
      while { ![eof $f] } \
      {
	puts [gets $f]
      }
      close $f
    }
    DashboardStart \
    {
      set FakeUpdate 0 
      if { $Model == "Nightly" && $argc == 3 } \
      {
	# If we are starting on "old" Nightly dashboard,
	# fake the Update.xml file
	if { [lindex $argv 2] != $NightlyDateStamp } \
	{
	  set FakeUpdate 1
	}
	set NightlyDateStamp [lindex $argv 2]
	set DashboardDir [file join $TestingBaseDir Dashboard ${NightlyDateStamp}-Nightly]
      }

      puts "Making $DashboardDir"
      file mkdir $DashboardDir
      set Dir [pwd]

      if { ![info exists [file join $DashboardDir Doxygen.xml]] } \
      {
	# Sorry, actually create the Doxygen.xml file...
	puts "\tBuilding Doxygen.xml"
	puts [exec $TclshCommand [file join $UtilitiesDir Doxygen.tcl] > [file join $Dir $DashboardDir Doxygen.xml]]
	# 	set f [open [file join $Dir $DashboardDir Doxygen.xml] w]
	# 	puts $f $XMLHeader
	# 	puts $f "<Doxygen></Doxygen>"
	# 	close $f
      }
      cd $SourceDirectory
      
      # If we don't have an existing update, make one
      if { ![info exists [file join $Dir $DashboardDir Update.xml]] } \
      {
	if { $FakeUpdate } \
	{
	  puts "Faking update"
	  set Update [open [file join $Dir $DashboardDir Update.xml] w]
	  puts $Update $XMLHeader
	  puts $Update "<Update></Update>"
	  close $Update
	} \
	else \
	{
	  # Actually do the update
	  # If the Model is Experimental, will do an update against the
	  # latest sources
	  # If the Model is Nightly, will update to the previous day's
	  # 23:00 repository snapshot
	  puts "\tBuilding Update.xml"
	  puts [exec $TclshCommand [file join $UtilitiesDir Update.tcl] $Model $NightlyDateStamp > [file join $Dir $DashboardDir Update.xml]]
	}
      }
      puts "\tCompleted"
      cd $Dir
    }
    DashboardEnd \
    {
      # Collect all the latest BuildStamps, and any from today's Nightlies

      # Make the images directory and copy things over
      # Icons need to go in BuildDir/Testing/HTML/TestingResults/Icons
      # From $SourceDir/Testing/HTML/TestingResults/Icons
      set DashboardIconsDir [file join $TestingBaseDir Icons]
      set IconsSourceDir [file join $SourceDirectory Testing HTML TestingResults Icons]
      if { ![info exists $DashboardIconsDir] } \
      {
	file mkdir [file join $DashboardIconsDir]
	set IconList [glob -nocomplain [file join $IconsSourceDir *]]
	foreach Icon $IconList \
	{
	  if { [file isdir $Icon] } \
	  {
	    continue
	  }
	  set TargetIcon [file join $DashboardIconsDir [file tail $Icon]]
	  set SourceIcon $Icon
	  # if { ![file exists [file join $DashboardIconsDir [file tail $Icon]]] }
	  if { ![file exists $TargetIcon] || [expr [file mtime $Icon] > [file mtime $TargetIcon]] } \
	  {
	    puts "Copy $Icon to $DashboardIconsDir"
	    # Copy it over.
	    file copy -force $Icon $DashboardIconsDir
	  }
	}
      }

      if { $Model == "Nightly" && $argc == 3 } \
      {
	# If we are starting on "old" Nightly dashboard
	set NightlyDateStamp [lindex $argv 2]
	set DashboardDir [file join $TestingBaseDir Dashboard ${NightlyDateStamp}-Nightly]
      } \
      else \
      {
	set DashboardDir [GetLastDashboardDirectory $Model]
	set NightlyDateStamp [string range [file tail $DashboardDir] 0 7]
      }

      set YesterdayDateStamp [GetYesterdayDateStamp $NightlyDateStamp]
      set DashboardTimeStamp [ExpandStamp $NightlyDateStamp-Nightly]
      set DashboardDate [clock format $DashboardTimeStamp -format "%A, %B %d %Y" -gmt 1]
      
      if { $DashboardDir == "" } \
      {
	puts stderr "Did not find a dashboard to end"
	exit 1
      }
      
      # Get the nightly builds relevant to this Dashboard
      set NightlyBuilds ""
      
      switch -glob $Model \
      {
	Nig* \
	{
	  # Get both the nightlies and the experimentals...
	  foreach f [glob -nocomplain -- [file join $TestingBaseDir Sites * * *]] \
	  {
	    # Ignore the coverage special directory...
	    if { [file tail $f] == "Coverage" } { continue }
	    if { [IsBuildStampInDay $NightlyDateStamp-Nightly [file tail $f]] } \
	    {
	      lappend NightlyBuilds $f
	    }
	  }
	}
	Exp* \
	{
	  # Just get the non-nightlies
	  set NightlyBuilds [glob -nocomplain -- [file join $TestingBaseDir Sites * * [MakeDateStamp]*]]
	}
      }
	  
      set Dir [pwd]
      cd $DashboardDir
      set DashboardStamp [file tail $DashboardDir]

      puts "Dashboard [file tail $DashboardDir]"
      if { [NeedToRemake Update] } \
      {
	puts -nonewline "\tBuilding Update.html "
      
	puts [catch { exec $JavaCommand -jar $Xalan -Q -IN Update.xml -OUT Update.html -XSL [file join $XSLDir Update.xsl] -PARAM TestDocDir [pwd] -PARAM DashboardDate $DashboardDate -PARAM DashboardStamp $DashboardStamp } Result]
	set Result [string trim $Result]
	if { $Result != "" } { puts "\t$Result" }
      }
      if { [NeedToRemake Doxygen] } \
      {
	puts -nonewline "\tBuilding Doxygen.html "
	puts [catch { exec $JavaCommand -jar $Xalan -Q -IN Doxygen.xml -OUT Doxygen.html -XSL [file join $XSLDir Doxygen.xsl] -PARAM DashboardDate $DashboardDate -PARAM TestDocDir [pwd] -PARAM DashboardStamp $DashboardStamp } Result]
	set Result [string trim $Result]
	if { $Result != "" } { puts "\t$Result" }
      }

      
      set DashboardTargets [open DashboardTargets.txt w]
      set DashboardFile [open Dashboard.xml w]

      puts $DashboardFile $XMLHeader
      puts $DashboardFile "<Dashboard>"

      # Add some information about the dashboard
      puts $DashboardFile "<Information>"
      puts $DashboardFile "\t<DashboardStamp>$DashboardStamp</DashboardStamp>"
      set Time [ExpandStamp [file tail $DashboardDir]]
      puts $DashboardFile "\t<Model>$Model</Model>"
      puts $DashboardFile "\t<GMT></GMT>"
      puts $DashboardFile "\t<LocalTime>[clock format [clock seconds]]</LocalTime>"
      puts $DashboardFile "\t<Yesterday>$YesterdayDateStamp-Nightly</Yesterday>"
      puts $DashboardFile "</Information>"

      XMLConcat $DashboardFile UpdateSummary.xml
      XMLConcat $DashboardFile DoxygenSummary.xml

      foreach NightlyBuild $NightlyBuilds \
      {
	puts "\tNightlyBuild $NightlyBuild"
	puts $DashboardTargets $NightlyBuild
	set CurrentDir [pwd]
	cd [file join $Dir $NightlyBuild]

	if { ![file exists [file join XML Build.xml]] && ![file exists [file join XML Test.xml]]} \
	{
	  puts "\t\tCould not find file [file join [pwd] XML Build.xml]"
	  cd $CurrentDir
	  continue
	}

	puts $DashboardFile "<BuildStamp>"
	
	foreach XML [list Build Test Coverage CoverageLog Purify] \
	{
	  if { ![file exists [file join XML $XML.xml]] } \
	  {
	    continue
	  }
	  puts $DashboardFile "\t<${XML}SubmissionDateTime>[clock format [file mtime [file join XML $XML.xml]]]</${XML}SubmissionDateTime>"

	  if { [NeedToRemake $XML] } \
	  {
	    puts -nonewline "\t\tBuilding $XML.html "
	    puts [catch { exec $JavaCommand -Xmx128m -jar $Xalan -Q -IN [file join XML $XML.xml] -OUT $XML.html -XSL [file join $XSLDir $XML.xsl] -PARAM TestDocDir [file join $Dir $NightlyBuild/]   -PARAM DashboardStamp $DashboardStamp -PARAM DashboardDate $DashboardDate } Result]
	    set Result [string trim $Result]
	    if { $Result != "" } { puts "\t$Result" }
	  }
	}
	
	cd $CurrentDir

	foreach Summary [list BuildSummary TestSummary CoverageSummary PurifySummary] \
	{
	  XMLConcat $DashboardFile [file join $Dir $NightlyBuild $Summary.xml]
	}
	puts $DashboardFile "</BuildStamp>"
	
      }
      puts $DashboardFile "</Dashboard>"

      close $DashboardFile
      close $DashboardTargets

      puts "Dashboard $DashboardStamp"
      # Now run xalan to build the dashboard
      foreach XML [list Dashboard TestOverview] \
      {
	if { [NeedToRemake $XML] } \
	{
	  set CreationDate [clock format [clock seconds]]
	  puts -nonewline "\tBuilding $XML.html "
	  flush stdout
	  puts [catch { exec $JavaCommand -jar $Xalan -Q -IN $XML.xml -OUT $XML.html.[pid] -XSL [file join $XSLDir $XML.xsl] -PARAM CreationDate $CreationDate -PARAM DashboardDate $DashboardDate -PARAM DashboardStamp $DashboardStamp -PARAM DashboardPath [pwd] } Result]
	  catch { file rename -force -- $XML.html.[pid] $XML.html }
	  set Result [string trim $Result]
	  if { $Result != "" } { puts "\t$Result" }
	} \
	else \
	{
	  puts "\tNo need to rebuild $XML.html"
	}
      }

      # Add the softlink from MostRecentResults to latest.
      # puts stderr [pwd]
      # cd $DashboardDir
      cd ..
      puts [pwd]
      file delete -force MostRecentResults-Nightly
      catch { exec ln -s [file tail $DashboardDir] MostRecentResults-Nightly } Result
      puts $Result
    }
  }
}
